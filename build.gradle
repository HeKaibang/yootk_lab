buildscript {
    repositories {
        // 使用阿里云仓库加速依赖下载
        maven { url 'https://maven.aliyun.com/repository/public' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:3.4.3'
    }
}

plugins {
    id 'java'
}

// 从 gradle.properties 中读取公共配置
group = project_group
version = project_version

// 子项目通用配置
subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    // 设置 JDK 版本兼容性
    sourceCompatibility = project_jdk
    targetCompatibility = project_jdk

    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public' }
        mavenCentral()
    }

    // 公共依赖管理
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.4.3"
        }
    }

    // 源代码目录配置
    sourceSets {
        main {
            java { srcDirs = ['src/main/java'] }
            resources { srcDirs = ['src/main/resources', "src/main/profiles/${System.getProperty('env') ?: 'dev'}"] }
        }
        test {
            java { srcDirs = ['src/test/java'] }
            resources { srcDirs = ['src/test/resources'] }
        }
    }

    // 测试配置
    test {
        useJUnitPlatform()
    }

    // 编码统一为 UTF-8
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc).configureEach {
        options.encoding = 'UTF-8'
    }
}

//默认启用测试任务
gradle.taskGraph.whenReady {
    tasks.each { task ->
        if (task.name.contains('test')) {
            task.enabled = true
        }
    }
}

project(':common') {
    // 公共模块无额外依赖
}

project(':lab_message') {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web:3.4.3'
        implementation project(':common')
    }
}